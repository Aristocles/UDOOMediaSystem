/* 
    UDOO Media System - March 2014 - Jay Aristocles (jay@enail.com.au)
    http://makeitbreakitfixit.com/2014/02/28/diy-complete-media-system-that-fits-in-the-palm-of-your-hand/
    See above link for full DIY instructions on how to build your own UDOO Media System
    This code is also available on GitHub under my account https://github.com/JaykGit/
    
    You may want to change the configuration below, but leaving at the defaults should work
    just fine. No other modifications needed. Default LCD is 2x16.
    
    CHANGELOG v1:
    
      - Initial version.
      
    KNOWN BUGS:
    
      - None that I am aware of. But surely there are many, so please email me if you find one.
    
    FUTURE IMPROVEMENTS:
    
      - (hardware and software): Perhaps include a RTC module so that the time isn't erased every time the Arduin is reset
    
    This program is free software: you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation, either version 3 of the License, or
    (at your option) any later version.
    
    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.
    
    You should have received a copy of the GNU General Public License
    along with this program.  If not, see <http://www.gnu.org/licenses/>.

    Credit to http://www.hacktronics.com/Tutorials/arduino-thermistor-tutorial for thermistor code.
*/

// START CONFIGURATION (do not edit anything above this line)
//
const int lcd_rows = 2, lcd_cols = 16; // Number of lines (rows) on the LCD screen (default: 2)
                                       // Number of characters (columns) per line on the LCD screen (default: 16)
const int serial_baud = 115200; // Serial baud speed. Must match the baud set in Linux script file
const int delay_lcd_temp = 1; // Time delay between LCD refreshes with the current temp (seconds)
const int max_temp = 45; // Maximum temp before the fan is switched on (celsius)
const int fan_run_length = 320; // Length of time the fan will stay on once triggered (seconds)
const int min_temp = 35; // Temp at which the fan will stop spinning, even if fan_run_length time not reached (celsius)
const int max_num_alerts = 5; // Maximum number of 'alerts' sent from Linux to hold and cycle through (max. 10)
const int scr_char = 25; // Total number of chars that be used in an alert sent to Arduino from Linux via serial.
                       // (eg. entire 16x2 screen has useable 32 chars). An extra char is added so the user
                       // can tell which alert pairs with which timestamp (as they are displayed separately. So if you
                       // have exactly 25 chars to spare for the alert, then variable should be set to 24 to avoid problems.
const int char_set = 0; // The animated character set can be changed. Currently only two to choose from. 0 or 1 (default: 0)
const int msg_display_len = 5; // Length of time to display each alert on LCD (seconds)
const int timestamp_display_len = 2; // Corresponding date/time for each alert is displayed after the alert (seconds)
//
// END CONFIGURATION (do not edit anything below this line)

#include <Time.h>
#include <LiquidCrystal.h>
#include <math.h>

// Digital Pins
#define LCD_BACKLIGHT 2 // LCD backlight pin
#define LCD_D7 3
#define LCD_D6 4
#define LCD_D5 5
#define LCD_D4 6
#define LCD_ENABLE 7
#define LCD_RW 8
#define LCD_RS 9
// Pin 10 unused
#define BUTTON 11 // Pushbutton
#define RELAY 12 // Relay pin
#define RESET 13 // Pin used to reset/restart the Arduino

// Analog Pins
#define THERMISTOR A0 // Analog pin for temp sensor

LiquidCrystal lcd(LCD_RS, LCD_RW, LCD_ENABLE, LCD_D4, LCD_D5, LCD_D6, LCD_D7);

void setup() {
  Serial.begin(serial_baud); // Serial sync speed. This needs to match in UDOO Linux for comms
  pinMode(RESET, OUTPUT); // Reset pin
  pinMode(RELAY, OUTPUT); // Relay switch for running 12V fan
  pinMode(LCD_BACKLIGHT, OUTPUT); // LCD backlilght pin. This is statically set using resistor
  pinMode(BUTTON, INPUT);
  digitalWrite(RELAY, LOW); // Initial state is off
  digitalWrite(LCD_BACKLIGHT, HIGH); // turn LCD backlight on
  digitalWrite(BUTTON, HIGH); // Turns internal pull-up resistor on
  lcd.begin(lcd_cols, lcd_rows); // Initialise LCD according to its size
  lcd.clear();                   // start with a blank screen
  delay(500);
  Serial.print("Starting UDOO Media System\n");
  if (max_num_alerts > 10)
    error("check serial", "Maximum number of alerts cannot exceed 10");
  buildLCDChars(); // Create custom chars for the LCD
}

void loop() {
  if (millis() > 4294960000) // millis() (a 32bit number) wrap detection
    delay(10000); // This will stop code from running for ~10 seconds to prevent
                  // timer problems due to millis() wrap during a timer running
                  // Yes, NOTHING will happen during this 10 seconds, but this should only
                  // occur once every ~50 days (~4,294,967 seconds).
                  // You can decrease the delay, but be sure to add to the 'if' statement
                  // however many milliseconds you remove from the delay.
                  // (eg. A delay of 5000 would mean you add the difference (5000) to 4294960000)
  double temp = drawTemp(delay_lcd_temp);
  checkButton();
  checkTemp(temp, fan_run_length, max_temp, min_temp);
  readSerial();
  drawLCD(msg_display_len, timestamp_display_len);
}

//
// FUNCTIONS:
//

void checkButton() {
  // I dont know what I need the button for just yet, so for now it will reset the Arduino
  int buttonState = digitalRead(BUTTON);
  if (buttonState == LOW) { // button pressed
    Serial.print("[BUTTON] - Button press detected\n");
    digitalWrite(RELAY, !digitalRead(RELAY));
    delay(1000);
    digitalWrite(RESET, LOW);
  }
}

boolean actionTrigger(unsigned long localTime, unsigned int triggerDelay) {
/*
  This function is designed to keep track of when to "do things"
  and notify the calling function when it is time to trigger an
  event. For example, if the temp sensor should be updated and
  shown on LCD screen every 4 seconds, this functions will notify
  when the 4 seconds is up.
  This function accepts two parameters.
  localTime - This is the time that we start counting from
  triggerDelay - This is the length of time we count (in seconds)
  When it is time to trigger an action, the function will return
  TRUE, otherwise it will return FALSE.  
*/
  if ((millis() >  localTime) && ((millis() - localTime) > (triggerDelay * 1000)))
    return true; // Check to ensure millis() hasn't wrapped;
                 // and also if it is time to trigger an action
  return false; // Otherwise, nothing changes
}

void error(String alert, String serialMsg) {
  int i = 1;
  while (i == 1) { // Infinite loop
  lcd.clear();
  lcd.setCursor(4,0);
  lcd.print("[ERROR]");
  lcd.setCursor(0,1);
  lcd.print(alert);
  Serial.print("[ERROR] - "); Serial.println(serialMsg);
  delay(1000);
  }
}

// **************************************************************** 
// ********************* LCD SCREEN FUNCTIONS ********************* 
// **************************************************************** 

void drawLCD(int timeDelay, int msgDelay) {
  // The code in this function is so damn ugly. There is a lot of reinventing of the wheel.
  // This needs to be cleaned up at a later stage. But for now, it works.
  static unsigned long lcdTimer; // Used for the alert timing
  static unsigned long lcdTimer2; // Used for the timestamp timing
  static int index = 1; // Keep track of which alert is displayed on LCD
  static boolean flip = false;
  int currMsg = (index%4);
  static unsigned long timestamp; // Used to hold the 32bit Unix epoch time

  switch(flip) { // Used to flip between showing the alert and its corresponding timestamp
    case true : {
      if (lcdTimer == 0)
        lcdTimer = millis();
      if (!actionTrigger(lcdTimer, msgDelay)) // Run the code every <delay> seconds
        return; // Exit function if it isnt time to run the code yet

      clearLCD((scr_char+1)); // Before we draw anything, lets clear only the alert section of the screen    
      Serial.print("[LCD] - Printing alert to screen\n");
      String* toDraw = getAlertToPrint(); // Get the strings to draw
      timestamp = convTimestamp(toDraw[0]); // Convert the timestamp from string to long
      for (int i = 0; i < lcd_rows; i++) {
        lcd.setCursor(0,i);
        lcd.print(toDraw[i+1]); // +1 is because [0] holds the timestamp
      }
      lcdTimer = 0; // Reset the trigger counte
      flip = (!flip); // Flip from 'alert' to 'timestamp' display
      drawAnimation(index);
      index++;      
      break;
    }
    case false : {
      if (lcdTimer2 == 0)
        lcdTimer2 = millis();
      if (!actionTrigger(lcdTimer2, timeDelay)) // Run the code every <delay> seconds
        return; // Exit function if it isnt time to run the code yet

      clearLCD((scr_char+1)); // Before we draw anything, lets clear only the alert section of the screen
      Serial.print("[LCD] - Printing timestamp to screen\n");
      lcd.setCursor(0,0);
      // Would be better to use breakTime() for converting from epoch to real time, but I couldnt figure it out. So doing it longer way
      lcdTime(day(timestamp)); lcd.print("/"); lcdTime(month(timestamp)); lcd.print(" | ");
//      int yr = year(timestamp); lcdTime(yr - 1970); // Print only the last 2 digits of the year
      lcdTime(hour(timestamp)); lcd.print(":"); lcdTime(minute(timestamp)); lcd.print(":"); lcdTime(second(timestamp));
      lcdTimer2 = 0;
      flip = (!flip); // Flip from 'alert' to 'timestamp' display
      drawAnimation(index);
      index++;
      break;
    }
  }
}

unsigned long convTimestamp(String strStamp) {
  // In order to convert the Unix epoch time we need to turn it in to a unsigned long first
  char epoch[32];
  strStamp.toCharArray(epoch, (strStamp.length()+1));
//  epoch[(strStamp.length()+1)] = NULL;
  return (atoi(epoch));
}

void lcdTime(int num) { // Add leading zero to numbers 0 to 9 for showing time/date
  if (num < 10)
    lcd.print("0");
  lcd.print(num);
}

void drawAnimation(int index) {
  int myRow = ((scr_char)/lcd_cols); // Find the very last char we can use by finding last row
  int myChar = ((scr_char)%lcd_cols); // and then last char on that row
  index = index%8; // There are only 8 chars in the character set, so we loop through them
  lcd.setCursor(myChar,myRow);
  lcd.write(byte(index)); // Clear single char
}

void buildLCDChars() {
  // Creating the custom characters to display on the LCD.
  // Initially I was going to use numbers but that looked ugly, so then I decided to use the good ol'
  // spinning line (using /, -, |, and \) but the character set in the LCD turns the backslash in to
  // a Japanese Yen symbole (apparently a common issue as many LCD's as manufactured with a different
  // character set to what us in the Western world would use. This can't be changed).
  // So ultimately I settled on creating my own character set. There is a maximum of 8 chars you can
  // make, so I decided on 4 different sequences each with its matching inverse/negative character.
  // The char will be used to display the alert while the inverse will be used to display the timestamp.
  // This way the user will be able to match an alert to a timestamp; plus the added benefit of pretty
  // graphics! It's a WIN/WIN!
  
  // Creating the new chars. Each byte represents a row of dots in the char. 0 = dot is off; 1 = dot is on.
  byte custChar0[8], custChar1[8], custChar2[8], custChar3[8], custChar4[8], custChar5[8], custChar6[8], custChar7[8];
  switch(char_set) {
    default :
      Serial.print("[WARN] - Invalid character set, using default\n");
    case 0 : {
      custChar0 = {B01100, B01100, B00000, B00000, B00000, B00000, B00000, B00000};
      custChar1 = {B10011, B10011, B11111, B11111, B11111, B11111, B11111, B11111};
      custChar2 = {B01100, B01100, B00110, B00110, B00000, B00000, B00000, B00000};
      custChar3 = {B10011, B10011, B11001, B11001, B11111, B11111, B11111, B11111};
      custChar4 = {B01100, B01100, B00110, B00110, B01100, B01100, B00000, B00000};
      custChar5 = {B10011, B10011, B11001, B11001, B10011, B10011, B11111, B11111};
      custChar6 = {B01100, B01100, B00110, B00110, B01100, B01100, B00110, B00110};
      custChar7 = {B10011, B10011, B11001, B11001, B10011, B10011, B11001, B11001};
      break;
    }
    case 1 : {
      custChar0 = {B10100, B11100, B00000, B00000, B00000, B00000, B00000, B00000};
      custChar1 = {B01011, B00011, B11111, B11111, B11111, B11111, B11111, B11111};
      custChar2 = {B10100, B11100, B00101, B00111, B00000, B00000, B00000, B00000};
      custChar3 = {B01011, B00011, B11010, B11000, B11111, B11111, B11111, B11111};
      custChar4 = {B10100, B11100, B00101, B00111, B10100, B11100, B00000, B00000};
      custChar5 = {B01011, B00011, B11010, B11000, B01011, B00011, B11111, B11111};
      custChar6 = {B10100, B11100, B00101, B00111, B10100, B11100, B00101, B00111};
      custChar7 = {B01011, B00011, B11010, B11000, B01011, B00011, B11010, B11000};
      break;
    }
  }
  // Store all the new chars in an array
  byte* allChars[8] = {custChar0, custChar1, custChar2, custChar3, custChar4, custChar5, custChar6, custChar7};

  for (int i = 0; i < 8; i++) // Push the chars in to the LCD for later use using lcd.write(byte(x));
    lcd.createChar(i, allChars[i]);
}

void clearLCD(int totalChars) {
  // Clears the specified number of chars starting from top left of LCD and spanning rows
  if (totalChars > (lcd_cols * lcd_rows)) // Make sure we don't clear more than the total num chars on LCD
    totalChars = (lcd_cols * lcd_rows);
  int rows = (totalChars/lcd_cols);
  int charsInLastRow = (totalChars%lcd_cols);
  for (int i = 0; i <  rows; i++) { // Clear the entire row
    for (int j = 0; j < lcd_cols; j++) {
      lcd.setCursor(j,i);
      lcd.print(" "); // Clear single char
    }
  }
  for (int i = 0; i < charsInLastRow; i++) { // Clear some chars in final row
    lcd.setCursor(i,rows);
    lcd.print(" ");
  }
}

// ************************************************************************ 
// ********************* THERMISTOR RELATED FUNCTIONS ********************* 
// ************************************************************************ 

double Thermister(int RawADC) {
  double temp = log(((10240000/RawADC) - 10000));
  // See http://en.wikipedia.org/wiki/Thermistor for explanation of formula
  temp = 1 / (0.001129148 + (0.000234125 * temp) + (0.0000000876741 * temp * temp * temp));
  temp = temp - 273.15;           // Convert Kelvin to Celcius
  return temp;
}

int drawTemp(int delay) {
  static unsigned long tempTimer;
  static int index;
  double temp = Thermister(analogRead(THERMISTOR));  // Read temp sensor
  if (tempTimer == 0)
    tempTimer = millis();
  if (!actionTrigger(tempTimer, delay)) // Run the code every <delay> seconds
    return temp; // Exit function if it isnt time to run the code yet

  tempTimer = 0; // Reset the trigger counter
  lcd.setCursor(10,1);
  if (temp < 10)
    lcd.print("0"); // Pad with a zero is less than 10
  lcd.print(temp);
  lcd.print("C");
  if (index == 0) {// We don't want to flood the serial with these msgs, so we stagger them
    Serial.print("[TEMP] - Temperature is ");
    Serial.print(temp);
    Serial.print(" C\n");
  }
  index = (index+1)%10;
  return temp;
}

void checkTemp(double temp, int fanTime, int maxTemp, int minTemp) {
  static unsigned long fanTimer;
  int delay = fanTime;
  if ((temp < maxTemp) && (digitalRead(RELAY) == LOW))
    return; // Nothing to see here, move along (exit function)
  if ((digitalRead(RELAY) == HIGH) && (temp < min_temp)) { // Fan already running but below min_temp threshold
    digitalWrite(RELAY, LOW); // Switch off fan regardless of fan timer state
    Serial.print("[FAN] - Switched off due to temp dropping below "); Serial.print(minTemp); Serial.print("C\n"); Serial.print("[FAN] - Timer reset\n");
    fanTimer = 0;
    return; // Exit function
  }
  if (fanTimer <= 0) {
    fanTimer = millis();
    Serial.print("[FAN] - Timer started. Fan will run for "); Serial.print(fanTime); Serial.print(" seconds, unless temp drops below "); Serial.print(minTemp); Serial.print("C first\n");
    digitalWrite(RELAY, HIGH); // Turn fan on    
  }
  if (actionTrigger(fanTimer, delay)) {
    digitalWrite(RELAY, LOW); // Timer expired, turn fan off
    Serial.print("[FAN] - Switched off due to timer expiring\n"); Serial.print("[FAN] - Timer reset\n");
    fanTimer = 0;
  } else if (millis() < fanTimer) { // millis() wrap detection
    digitalWrite(RELAY, LOW); // This is needed as the wrap detection in loop() is
                              // too short to help us here.
    Serial.print("[FAN] - Switched off due to millis() wrap error prevention\n"); Serial.print("[FAN] - Timer reset\n");
    fanTimer = 0;
  }
}

// ****************************************************************** 
// ********************* SERIAL COMMS FUNCTIONS ********************* 
// ****************************************************************** 

// Once we put this code in to its own library we can place the below global variables inside the Constructor
// I am keeping these global variables here so that all serial comms related code is in one place
char arrAlerts[max_num_alerts][scr_char+1] = {"UDOO MediaSystem"}; // 2D array to store rotating alerts sent to us by Linux through serial
time_t arrTimestamp[max_num_alerts]; // Corresponding array to hold timestamps for alerts
int currIndex = 0; // Used to keep track of which index to use in the array
String currAlert[(lcd_rows+1)]; // First element is always timestamp, then followed by an element for each LCD row
                                // This variable is used only for returning data to be displayed on LCD screen

void readSerial() {
/*
  Our serial protocol is simple. All strings to be parsed must begin with { and end with }
  and contain no more than scr_char characters
  All received strings should coming inbound already sanitised by way of whatever script we
  are using in Linux. However, this program does some of its own sanitising too.
  All received strings are placed inside an array which is FIFO rotated. Number of slots in
  the array is determined by max_num_alerts configuration option.
  
  A ! followed immediately after the { is the "delete flag". This means that the string
  should be marked for deletion if an exact match is found in the array.
  
  If a duplicate alert is received then the old alert is deleted and the new alert is
  added at the head of the array, thereby extending its life in the FIFO rotation.
  [NOTE: FIFO is not working. Currently no re-ordering is done]
  
  All alerts stored in the array also have a timestamp added by the Arduino code.
  An @ followed immediately after the { is the "time stamp" flag. This means a time stamp is
  to be found afterwards in the format DD,MM,YYYY.hh,mm,ss and ending with the }
*/
  if (Serial.find("{")) { // Our protocol requires strings to be printed on LCD to begin with {
    char serialData[(scr_char+1)]; // Num of chars to display on screen. The +1 is to hold the NULL terminator
    int typeFlag = 0; // Type 0. Add alert (default)
    Serial.print("[SERIAL] - Serial comms detected\n"); // This line must be here or .peek() wont work properly
    switch(Serial.peek()) { // .peek() only returns ASCII decimal
      case 33: // Looking for "!". ASCII 33 == !
        Serial.print("[SERIAL] - Delete flag detected\n");
        Serial.read(); // Read the char so it leaves the serial buffer
        typeFlag = 1; // Type 1. Delete alert
        break;
      case 64: // Looking for "@". ASCII 64 == @
        Serial.print("[SERIAL] - Timestamp flag detected\n");
        Serial.read(); // Read the char so it leaves the serial buffer
        // No error checking done. Be sure the correct time/date format is sent!
        timeDateSet(Serial.parseInt(), Serial.parseInt(), Serial.parseInt(), Serial.parseInt(), Serial.parseInt(), Serial.parseInt());
        return; // Exit function. Time is already set and nothing else left to do
        break; // We shouln't hit this code, but here for completeness
      case 63: // Looking for "?". ASCII 63 == ?
        Serial.print("[SERIAL] - Data dump signal detected. Dumping array data to serial\n");
        while (Serial.available())
          Serial.read();
        typeFlag = 2; // Type 2. Data dump
        break;
    }
    int numChars = Serial.readBytesUntil('}', serialData, scr_char); // Store chars until } is found
    serialData[numChars] = NULL; // Add the NULL terminator
    Serial.print("[SERIAL] - "); Serial.print(numChars); Serial.print(" chars captured. Parsed as: "); Serial.print(serialData); Serial.println();
    switch(typeFlag) {
      case 0: // All alerts need to be processed first
      case 1:
        findAlert(numChars, typeFlag, serialData);
        break;
      case 2:
        dataDump(); // Dump contents of array to serial
        break;
    }
  }
}

void dataDump() {
  Serial.print("Dumping entire contents of alerts array to serial\n");
  Serial.print("--------------------\n");
  for (int i=0; i < max_num_alerts; i++) {
    // Would be better to use breakTime() for converting from epoch to real time, but I couldnt figure it out. So doing it longer way
    Serial.print("Index ["); Serial.print(i); Serial.print("] {"); printTime(day(arrTimestamp[i])); Serial.print("/");
    printTime(month(arrTimestamp[i])); Serial.print("/"); printTime(year(arrTimestamp[i])); Serial.print(" - "); printTime(hour(arrTimestamp[i]));
    Serial.print(":"); printTime(minute(arrTimestamp[i])); Serial.print(":"); printTime(second(arrTimestamp[i])); Serial.print("} - ");
    for (int j = 0; j < (scr_char+1); j++)
      Serial.print(arrAlerts[i][j]);
    Serial.println();
  }
  Serial.print("Next write to index "); Serial.print(currIndex%max_num_alerts); Serial.print(" next\n");
  Serial.print("--------------------\n");
}

String* getAlertToPrint() {
/*
  This functions will sanitise and return the text to be printed to the LCD
  The text is placed inside an array of Strings where each array element
  contains texts for one row on the LCD.
  The first array element is always the Unix epoch timestamp which corresponds
  to the alert.
  This function will rotate and keep track of which alert to be presented.
*/
  static int counter = 0; // This is used to keep track of which alert+timestamp to fetch and sanitise
  for (int i = 0; i < (lcd_rows+1); i++) // First we clear out previous data from the array
    currAlert[i] = "";
  currAlert[0] = String(arrTimestamp[counter]); // Timestamp always goes in first element slot
  String tmpGlob = String(arrAlerts[counter]); // Placing the alert in temp string for processing
  int globLength = tmpGlob.length();
  if (globLength > (lcd_rows * lcd_cols)) // Number of chars in the alert should never more than the number 
    error("too many chars", "Alerts cannot exceed total number of chars on the LCD");
  int rowsNeeded = globLength / lcd_cols; // Calculating numbers of rows needed to display the alert
  if ((globLength%lcd_cols) > 0) // If we get a modulo result that is not 0, this means there are more chars
    rowsNeeded++; // So we add another row
  for (int i = 0; i < rowsNeeded; i++) {
    int startPosition = (lcd_cols * i);
    int endPosition = ((lcd_cols * i) + lcd_cols);
    if (endPosition > globLength)
      endPosition = globLength; // Make sure we stay within the bounds of the string
    currAlert[i+1] = tmpGlob.substring(startPosition, endPosition);
  }
  counter = (counter+1)%max_num_alerts;
  int looper = 0; // Used for below 'while' loop only, to make sure we dont get stuck in infinite loop 
    while (arrAlerts[counter][0] == NULL) {
    counter = (counter+1)%max_num_alerts; // Finding the next non-empty element to use
    if (looper > max_num_alerts)
      return currAlert; // No more elements with data found, exit to avoid inifite loop
    looper++;
  } 
  return currAlert; 
}

void findAlert(int numChars, int type, char serialData[]) {
  String receivedData(serialData); // Convert to String for easier comparison
  for (int i=0; i < max_num_alerts; i++) {
    if (arrAlerts[i][0] != NULL) { // Only run below code if an element exists in the array
      char tempArr[(scr_char+1)];
      for (int j = 0; j < (scr_char+1); j++)
        tempArr[j] = arrAlerts[i][j]; // Building the entire string to tempArr for comparison
      String existingData(tempArr);
      if (existingData.equals(receivedData)) { // Comparing strings, if match found then we delete the array entry
        Serial.print("[DEBUG] - Duplicate alert found in array, deleting index "); Serial.print(i); Serial.println();
        deleteAlert(i);
      }
    }
  }
  switch(type) {
    case 0: // If flag is set to 0, then we add the entry to the array
      addAlert(numChars, serialData);
      break;
  }
}

int addAlert(int numChars, char serialData[]) {
  deleteAlert(currIndex%max_num_alerts); // We need to make sure the array element is empty before we use it
  Serial.print("[DEBUG] - Adding alert to array at index "); Serial.print(currIndex%max_num_alerts); 
  for (int i = 0; i < numChars; i++) {
    arrAlerts[currIndex%max_num_alerts][i] = serialData[i];
    arrAlerts[currIndex%max_num_alerts][i+1] = NULL; // Add a NULL to the end of the chars
  }
  arrTimestamp[currIndex%max_num_alerts] = now(); // Grab current date/time in epoch format. Stored in corresponding array
  Serial.print(" with epoch timestamp: "); Serial.print(arrTimestamp[currIndex%max_num_alerts]); Serial.println();
  currIndex++; // Increment index in preparation for next incoming alert
}

void deleteAlert(int index) {
  for (int i = 0; i < (scr_char+1); i++) // Delete alert array
    arrAlerts[index][i] = NULL;
  arrTimestamp[index] = NULL; // Delete corresponding timestamp for that alert
}

void timeDateSet(int sday, int smonth, int syear, int shour, int sminute, int ssecond) {
/*
  Time is periodically _pushed_ to the Arduino from Linux. Arduino will never pull the
  time. So this means you need to make sure that Linux is configured to send the time
  to Arduino, otherwise you will have timestamps that begin from Unix epoch.
  
  Data we receive should be in the format DD,MM,YYYY.hh,mm,ss
  We expect the date/time to be in the correct format. No sanity checking is
  done here.
  
  NOTE: All timer-based time keeping uses millis() and not this clock. Therefore,
  the time being set correctly is _optional_. This time is used to timestamp
  alerts that are received through from Linux for displaying on screen.
*/ 
  setTime(shour, sminute, ssecond, sday, smonth, syear);
  Serial.print("[SERIAL] - Time/Date set: "); printTime(hour()); Serial.print(":"); printTime(minute()); Serial.print(":"); printTime(second());
  Serial.print(" - "); printTime(day()); Serial.print("/"); printTime(month()); Serial.print("/"); printTime(year()); Serial.println();
}

void printTime(int num) { // Add leading zero to numbers 0 to 9 for showing time/date
  if (num < 10)
    Serial.print("0");
  Serial.print(num);
}
